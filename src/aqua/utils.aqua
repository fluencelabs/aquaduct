aqua Utils declares *

-- moves due to implicit canonicalization on the last line
func optOr(opt: ?string, or: string) -> string:
  res: *string
  if opt == nil:
    res <<-or
  else:
    res <<- opt!
  <- res!

-- moves due to implicit canonicalization on the last line
func contains(haystack: []string, needle: string) -> bool:
  c: *bool
  for t <- haystack:
    if t == needle:
      c <<- true
  c <<- false
  <- c!

-- add is a scalar, so no canonicalization happens, no move
func pushAll(stream: *string, add: []string):
  for a <- add:
    stream <<- a

-- add is a scalar, so no canonicalization happens, no move
func appendToMapKey(map: %string, add: []string, key: string):
  for a <- add:
    map <<- key, a  

func mergeMaps(map: %string, push: %string):
  for k, v <- push:
    map <<- k, v     

-- Should not canonicalize, but not checked
func mapToStream(map: %string, stream: *string):
  for k <- map.keysStream() rec:
    for v <- map.getStream(k) rec:
      stream <<- v

-- moves, as returns array
func prepend(prefix: string, stream: []string) -> []string:
  new_stream = *[prefix]
  for a <- stream:
    new_stream <<- a
  <- new_stream