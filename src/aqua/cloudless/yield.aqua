aqua Cloudless.Yield declares *

import Worker from "@fluencelabs/aqua-lib/subnet.aqua"
import Promise, Error from "../promise"
import pushAll, appendToMapKey, mapToStream from "../utils"
import Logger, prefixed from "../logger"

alias WorkerYield: Worker -> ?Error
alias Yield: *Worker, %Error, WorkerYield -> Promise

--
-- Yielding Strategies for Cloudless Functions â€“ distinguishing success state from error state
--

const FATAL = "fatal"

-- Wait for K replies, no matter the quality of replies, success/err
func wait_for{Logger}(success: u8, error: u8) -> Yield:
  l2 = prefixed{Logger}("Yield.wait_for")
  log = l2.log

  wait = func (ws: *Worker, err: %Error, cb: Worker -> ?Error) -> Promise:
    yield = func () -> ?Error:
      ok: *bool
      log(["is called"])
      
      -- I hope that it will not canonicalize ws, need to check
      for w <- ws rec:
        log(["Got a worker", w])
        e <- cb(w)
        log(["Errors are", e])
        if e == nil:
          ok <<- true
        else:  
          appendToMapKey(err, e, w.worker_id!)

      -- TODO not absolutely correct:
      -- ignores errors that happen BEFORE we can reach workers
      -- but they might be fatal
      ks = err.keysStream()
      fs = err.getStream(FATAL)

      join ok[success-1]
      par join ks[error-1]
      par join fs!

      log(["Joined!", fs, ks, ok])

      new_error: *Error
      if err.keys().length >= error:
        mapToStream(err, new_error)
        --log(["Finally, errored!", new_error])
      if err.contains(FATAL):
        pushAll(new_error, err.get(FATAL))  

      <- new_error
    <- Promise(yield = yield)
  <- wait